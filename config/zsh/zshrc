# If not in tmux, start tmux.
# if [[ -z ${TMUX+X}${ZSH_SCRIPT+X}${ZSH_EXECUTION_STRING+X} ]]; then
#   exec tmux
# fi

function zcompile-many() {
  local f
  for f; do zcompile -R -- "$f".zwc "$f"; done
}

plugin_dir=$HOME/.local/share/zsh/plugins

# Clone and compile to wordcode missing plugins.
if [[ ! -e $plugin_dir/zsh-syntax-highlighting ]]; then
  git clone --depth=1 https://github.com/zsh-users/zsh-syntax-highlighting.git "$plugin_dir"/zsh-syntax-highlighting
  zcompile-many "$plugin_dir"/zsh-syntax-highlighting/{zsh-syntax-highlighting.zsh,highlighters/*/*.zsh}
fi
if [[ ! -e $plugin_dir/zsh-autosuggestions ]]; then
  git clone --depth=1 https://github.com/zsh-users/zsh-autosuggestions.git "$plugin_dir"/zsh-autosuggestions
  zcompile-many "$plugin_dir"/zsh-autosuggestions/{zsh-autosuggestions.zsh,src/**/*.zsh}
fi
if [[ ! -e $plugin_dir/powerlevel10k ]]; then
  git clone --depth=1 https://github.com/romkatv/powerlevel10k.git "$plugin_dir"/powerlevel10k
  make -C "$plugin_dir"/powerlevel10k pkg
fi
if [[ ! -e $plugin_dir/zsh-vi-mode ]]; then
  git clone --depth=1 https://github.com/jeffreytse/zsh-vi-mode.git "$plugin_dir"/zsh-vi-mode
  zcompile-many "$plugin_dir"/zsh-vi-mode/zsh-vi-mode.zsh
fi

# Activate Powerlevel10k Instant Prompt.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Add custom completions to fpath.
fpath=($HOME/.local/share/zsh/completions $fpath)

# Enable the "new" completion system (compsys).
autoload -Uz compinit && compinit
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
[[ ~/.zcompdump.zwc -nt ~/.zcompdump ]] || zcompile-many ~/.zcompdump
unfunction zcompile-many

ZSH_AUTOSUGGEST_MANUAL_REBIND=1

# Load plugins.
source "$plugin_dir"/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source "$plugin_dir"/zsh-autosuggestions/zsh-autosuggestions.zsh
source "$plugin_dir"/powerlevel10k/powerlevel10k.zsh-theme
source "$plugin_dir"/zsh-vi-mode/zsh-vi-mode.zsh

# Fzf integration.
command -v fzf &>/dev/null && eval "$(fzf --zsh)"

# History Options
setopt auto_pushd # Automatically use pushd when using cd.
setopt pushd_ignore_dups

setopt noclobber # Forbid '>' from truncating files

setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_no_store
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt hist_verify
setopt inc_append_history

# Increase history size
export HISTSIZE=100000
export SAVEHIST=100000
export HISTFILE=~/.local/share/zsh/zsh_history

# Ignore commands for the history.
export HISTIGNORE="(* --help|man|man *|cd|cd -|pwd|date|exit|ls|lg|ll|lll|l|lt|eza)"

# Load config.
source "$DOTFILES_ZSH"/config/p10k.zsh
source "$DOTFILES_ZSH"/config/aliases.zsh
source "$DOTFILES_ZSH"/config/keybindings.zsh
source "$DOTFILES_ZSH"/config/autols.zsh

# Load local config.
[[ -f $LOCAL_CONFIG/zshrc ]] && source "$LOCAL_CONFIG/zshrc"
