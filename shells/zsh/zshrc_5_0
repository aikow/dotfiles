# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi


#######################################################################
#                               Exports                               #
#######################################################################

# If you come from bash you might have to change your $PATH.
export DOTFILES="$HOME/.dotfiles"
export PATH="${HOME}/.bin:${HOME}/.local/bin:/usr/local/bin:/opt/vc/bin:${PATH}"
export AWS_SHARED_CREDENTIALS_FILE=/root/.aws/credentials

### Editor ###
if command -v nvim &>/dev/null; then
  export VISUAL=nvim
else;
  export VISUAL=vim
fi
export EDITOR=${VISUAL}

### FZF ###
if command -v fd &>/dev/null; then
  export FZF_DEFAULT_COMMAND='fd --type file --follow --hidden --exclude .git --color=always'
  export FZF_CTRL_T_COMMAND="${FZF_DEFAULT_COMMAND}"
  export FZF_DEFAULT_OPTS='--ansi'
elif command -v rg &>/dev/null; then
  export FZF_DEFAULT_COMMAND='rg --files --no-ignore --hidden --follow --glob "!.git/*"'
fi
export FZF_DEFAULT_OPTS="${FZF_DEFAULT_OPTS} --bind ctrl-f:preview-half-page-down,ctrl-b:preview-half-page-up"


#######################################################################
#                                 zinit                               #
#######################################################################

source "${HOME}/.zinit/bin/zinit.zsh"

# Plugins
zinit snippet OMZL::git.zsh
zinit snippet OMZP::git
zinit snippet OMZP::colored-man-pages
zinit snippet OMZP::command-not-found

zinit light commanda-panda/zsh-auto-ls

# These 2 must be in this order
zinit load zsh-users/zsh-autosuggestions
zinit load zsh-users/zsh-syntax-highlighting
zinit load zsh-users/zsh-history-substring-search

zinit wait lucid atload"zicompinit; zicdreplay" blockf for \
    zsh-users/zsh-completions

# Theme
zinit ice depth"1"
zinit light bhilburn/powerlevel9k

POWERLEVEL9K_PROMPT_ON_NEWLINE=true
POWERLEVEL9K_PROMPT_ADD_NEWLINE=true
POWERLEVEL9K_LEFT_PROMPT_ELEMENTS=(vi_mode context dir )
POWERLEVEL9K_RIGHT_PROMPT_ELEMENTS=(status anaconda virtualenv time)

POWERLEVEL9K_VI_INSERT_MODE_STRING="I"
POWERLEVEL9K_VI_COMMAND_MODE_STRING="N"
POWERLEVEL9K_SHORTEN_DIR_LENGTH=3
POWERLEVEL9K_SHORTEN_DELIMITER="..."
POWERLEVEL9K_SHORTEN_STRATEGY="truncate_from_left"
POWERLEVEL9K_DIR_SHOW_WRITABLE=true


#######################################################################
#                               History                               #
#######################################################################

# History Options
setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt hist_verify

# Share history across all your terminal windows
setopt share_history
setopt noclobber

# Increase history size
export HISTSIZE=100000
export SAVEHIST=100000
export HISTFILE=~/.zsh_history
export HISTIGNORE="ls:ll:cd:cd -:pwd:exit:date:* --help"


#######################################################################
#                         User Configuration                          #
#######################################################################
#
# Bind C-e to edit the command line
autoload edit-command-line; zle -N edit-command-line
bindkey '^x^e' edit-command-line

# Bind C-o to add an argument to the previous command
bindkey -s '^o' '^[k0Ea '

# Bind C-x C-l to pipe the previous command into less
bindkey -s '^x^l' '^[kA | less'

# Bind C-x C-p to pipe the previous command
bindkey -s '^p' '^[kA | '

# History seach
bindkey "^k" history-beginning-search-backward
bindkey "^j" history-beginning-search-forward

# Clear screen
bindkey "^l" clear-screen

source "${DOTFILES}/shells/functions/fzf-git.sh"
join-lines() {
  local item
  while read item; do
    echo -n "${(q)item} "
  done
}

() {
  local c
  for c in $@; do
    eval "fzf-g$c-widget() { local result=\$(_g$c | join-lines); zle reset-prompt; LBUFFER+=\$result }"
    eval "zle -N fzf-g$c-widget"
    eval "bindkey '^g^$c' fzf-g$c-widget"
  done
} f b t r h s

# Load completions
autoload -U compinit && compinit
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'


#######################################################################
#                               Aliases                               #
#######################################################################
#
# Load OS specific settings
[[ -f "${DOTFILES}/shells/zsh/os/${OSTYPE}.zsh" ]] && source "${DOTFILES}/shells/zsh/os/${OSTYPE}.zsh" 

# Source fzf if installed
[[ -f ~/.fzf.zsh ]] && source ~/.fzf.zsh

# Load user aliases
[[ -f "${DOTFILES}/shells/zsh/aliases" ]]  && source "${DOTFILES}/shells/zsh/aliases"

# Load local settings and aliases
[[ -f "${HOME}/.aliases.local" ]] && source "${HOME}/.aliases.local"
[[ -f "${HOME}/.zshrc.local" ]] && source "${HOME}/.zshrc.local"

